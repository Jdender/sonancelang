use std::str::FromStr;
use crate::ast::*;

grammar;

match {
    "func", "->", "let", ";", // Function

    "I32", "F32", // Types

    "=", // Special Operators

    "(", ")", "{", "}", // Parens

    "+", "-", "*", "/", // Numeric Operators

    // Regex Tokens
    r"[a-zA-Z_]\w*" => IDENTIFIER,
    r"[+-]?\d+" => INT_LITERAL,
    r"[+-]?\d+\.\d+" => FLOAT_LITERAL,
}

#[inline]
Boxed<T>: Box<T> = T => Box::new(<>);

pub File: File = "func" <name: Identifier>  "(" ")" "->" <return_type: Type> <body: Block> => File { <> };

Identifier: Identifier = IDENTIFIER => Identifier::new(<>.to_string());

Type: Type = {
    "I32" => Type::I32,
    "F32" => Type::F32,
}

Block: Block = "{" <body: Statement*> <trailing: Boxed<Expression>> "}" => Block { <> };

Statement: Statement = {
    "let" <place: Identifier> "=" <value: Expression> ";" => Statement::LetBinding { <> },
    <Expression> ";" => Statement::SideEffect(<>),
    <ExpressionWithBlock> => Statement::SideEffect(<>),
}

ExpressionWithBlock: Expression = {
    Block => Expression::Block(<>),
}

Expression = {
    <place: Identifier> "=" <value: Boxed<Expression>> => Expression::Assignment { <> },
    ExpressionWithBlock,
    ExprPre2,
}

ExprPreN<X, Op, Y>: Expression = {
    <left: Boxed<X>> <operator: Op> <right: Boxed<Y>> => Expression::InfixCall { <> },
    Y,
}

ExprPre2 = ExprPreN<ExprPre2, OpPre2, ExprPre1>;

OpPre2: InfixOperator = {
    "+" => InfixOperator::Add,
    "-" => InfixOperator::Subtract,
}

ExprPre1 = ExprPreN<ExprPre1, OpPre1, ExprPre0>;

OpPre1: InfixOperator = {
    "*" => InfixOperator::Multiply,
    "/" => InfixOperator::Divide,
}

ExprPre0: Expression = {
    <operator: PrefixOperator> <value: Boxed<ExprPre0>> => Expression::PrefixCall { <> },
    "(" <Expression> ")",

    Literal => Expression::Literal(<>),
    Identifier => Expression::Lookup(<>),
}

PrefixOperator: PrefixOperator = {
    "-" => PrefixOperator::Negate,
}

Literal: Literal = {
    INT_LITERAL => Literal::I32(i32::from_str(<>).unwrap()),
    FLOAT_LITERAL => Literal::F32(f32::from_str(<>).unwrap()),
}
